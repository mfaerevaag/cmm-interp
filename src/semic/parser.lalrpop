use std::str::FromStr;
use ast::*;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, (usize, &'input str), ()>>);


// macros

Comma<T>: Vec<T> = {
    <h:(<T> ",")*> <t:T?> =>
        h.into_iter().chain(t).collect()
};

// token precedence

match {
    "return",
    "extern",
    "if",
    "else",
    "while",
    "for",
    "continue",
    "break",

    "void",
    "int",
    "char",
    "float",

    "printf",

} else {
    r"[0-9]+" => TInt,
    r"[0-9]+\.[0-9]+" => TFloat,
    r#""(.|([\\]0|([\\]n)|([\\]t)))*""# => TString,
    r#"('(.|([\\]0|([\\]n)|([\\]t)))')"# => TChar,

} else {
    r"[a-zA-Z][a-zA-Z0-9_]*" => TIdent,

    _
}

// production rules

pub Prog: CProg<'input> = <l:(<ProgElem>)*> => {
    l.into_iter().flat_map(|x| x.to_vec()).collect()
};

ProgElem: Vec<CProgElem<'input>> = {
    // decl
    <l:@L> <t:Type> <m:VarArray> <mm:("," <VarArray>)*> ";" <r:@R> => {
        vec![m].into_iter().chain(mm).map(|(id, so)| {
            CProgElem::Decl((l, r), t.clone(), id, so)
        }).collect()
    },

    // proto
    Proto,

    // func
    Func,
};

Proto: Vec<CProgElem<'input>> = {
    <l:@L> "void" <m:Comma<FuncDecl>> ";" <r:@R> => {
        m.into_iter().map(|(id, ps)| {
            CProgElem::Proto((l, r), CProto {
                ret: None,
                name: id,
                params: ps,
            })
        }).collect()
    },
    <l:@L> <t:Type> <p:("*")?> <m:Comma<FuncDecl>> ";" <r:@R> => {
        let tt = match p {
            None => Some(t.clone()),
            _ => Some(CType::Ref(Box::new(t.clone()))),
        };
        m.into_iter().map(|(id, ps)| {
            CProgElem::Proto((l, r), CProto {
                ret: tt.clone(),
                name: id,
                params: ps,
            })
        }).collect()
    },
};

pub Func: Vec<CProgElem<'input>> = {
    <l:@L> "void" <d:FuncDecl> <s:FuncBody> <r:@R> => {
        let (id, param) = d;
        vec![CProgElem::Func((l, r), CFunc {
            proto: CProto {
                ret: None,
                name: id,
                params: param,
            },
            body: s,
        })]
    },
    <l:@L> <t:Type> <p:("*")?> <d:FuncDecl> <s:FuncBody> <r:@R> => {
        let tt = match p {
            None => Some(t.clone()),
            _ => Some(CType::Ref(Box::new(t.clone()))),
        };
        let (id, param) = d;
        vec![CProgElem::Func((l, r), CFunc {
            proto: CProto {
                ret: tt,
                name: id,
                params: param,
            },
            body: s,
        })]
    },
};

FuncDecl: (CIdent<'input>, Vec<(CType, CIdent<'input>)>) = {
    <id:Ident> "(" <param:FuncParam> ")" => (id, param),
};

FuncParam: Vec<(CType, CIdent<'input>)> = {
    Comma<Param>,
    "void" => Vec::new(),
};

Param: (CType, CIdent<'input>) = {
    <t:Type> <p:("*")*> <r:Ident> <s:("[" "]")*> => {
        let mut tt = t;
        for i in 0..(p.len() + s.len()) {
            tt = CType::Ref(Box::new(tt));
        }
        (tt, r)
    }
};

FuncBody: CStmt<'input> = {
    <l:@L> "{" <stmt:(<Stmt>)*> "}" <r:@R> =>
        CStmt::Block((l, r), stmt.into_iter().flat_map(|x| match x {
            CStmt::Block(_, ss) => ss,
            x => vec![Box::new(x)]
        }).collect())
};

// statements

pub Stmt: CStmt<'input> = {
    Stmt_ => *<>,
};

Stmt_: Box<CStmt<'input>> = {
    StmtIfMatching,
    StmtIfUnmatching,
};

StmtIfMatching: Box<CStmt<'input>> = {
    <l:@L> "if" "(" <cond:Expr> ")" <stmt:StmtIfMatching> "else" <stmt2:StmtIfMatching> <r:@R> =>
        Box::new(CStmt::If((l, r), cond, stmt, Some(stmt2))),
    StmtOther,
};

StmtIfUnmatching: Box<CStmt<'input>> = {
    <l:@L> "if" "(" <cond:Expr> ")" <stmt:StmtIfMatching> <r:@R> =>
        Box::new(CStmt::If((l, r), cond, stmt, None)),
    <l:@L> "if" "(" <cond:Expr> ")" <stmt:StmtIfMatching> "else" <stmt2:StmtIfUnmatching> <r:@R> =>
        Box::new(CStmt::If((l, r), cond, stmt, Some(stmt2))),
};

StmtOther: Box<CStmt<'input>> = {
    // decl
    <StmtDecl> ";",

    // assignment
    <StmtAssign> ";",

    // call
    <l:@L> <i:Ident> "(" <p:Comma<Expr1>> ")" ";" <r:@R> => Box::new(CStmt::Call((l, r), i, p)),

    // return
    <l:@L> "return" <e:Expr> ";" <r:@R> =>
        Box::new(CStmt::Return((l, r), Some(e))),
    <l:@L> "return" ";" <r:@R> =>
        Box::new(CStmt::Return((l, r), None)),

    // block
    <l:@L> "{" <s:(<Stmt_>)*> "}" <r:@R> =>
        Box::new(CStmt::Block((l, r), s)),

    // while
    <l:@L> "while" "(" <cond:Expr> ")" <stmt:StmtIfMatching> <r:@R> =>
        Box::new(CStmt::While((l, r), cond, stmt)),

    // for
    <l:@L> "for" "(" <init:StmtAssign?> ";" <cond:Expr?> ";" <inc:StmtAssign?> ")" <stmt:StmtIfMatching> <r:@R> => {
        let mut top = vec![];
        match init {
            Some(s) => top.push(s),
            None => {}
        };

        let cond = match cond {
            Some(e) => e,
            None => CExpr::Int((l, r), 1),
        };

        let body = match inc {
            Some(s) => {
                let mut ss = vec![];
                match *stmt {
                    CStmt::Block(_, s) => {
                        ss.extend(s);
                    },
                    _ => ss.push(stmt),
                };
                ss.push(s);
                Box::new(CStmt::Block((0,0), ss))
            },
            None => stmt,
        };

        // TODO: fix locations
        let whiles = CStmt::While((l, r), cond, body);

        top.push(Box::new(whiles));

        Box::new(CStmt::Block((l, r), top))
    },

    // print
    <l:@L> "printf" "(" <e:Expr> ")" ";" <r:@R> =>
        Box::new(CStmt::Print((l, r), e)),

    // error
    // ! => { errors.push(<>); Box::new(CStmt::Error) },
};

StmtDecl: Box<CStmt<'input>> = {
    <l:@L> <t:Type> <m:Comma<StmtDecl2>> <r:@R> => {
        let mut x: Vec<Box<CStmt<'input>>> = m.into_iter().map(|(id, so, eo)| {
            let mut stmts = vec![];
            let tt = match so {
                Some(_) => CType::Ref(Box::new(t.clone())),
                _ => t.clone()
            };
            let s = so.unwrap_or(None);

            stmts.push(Box::new(CStmt::Decl((l, r), tt, id, s.clone())));
            // also init?
            if let Some(e) = eo {
                stmts.push(Box::new(CStmt::Assign((l, r), id, s, e)));
            }

            stmts
        }).flat_map(|x| x.to_vec()).collect(); // flatten

        match x.len() {
            1 => x.pop().unwrap(),
            _ => Box::new(CStmt::Block((l, r), x))
        }
    },
};

StmtDecl2: (CIdent<'input>, Option<Option<CExpr<'input>>>, Option<CExpr<'input>>) = {
    <l:Ident> <eo:("=" <Expr>)?> => (l, None, eo),
    <l:Ident> "[" <so:Expr?> "]" <eo:("=" <Expr>)?> => (l, Some(so), eo),
    "*" <l:Ident> <eo:("=" <Expr>)?> => (l, Some(None), eo),
};

VarArray: (CIdent<'input>, Option<CExpr<'input>>) = {
    <l:Ident> <so:("[" <Expr> "]")?> => (l, so),
};

StmtAssign: Box<CStmt<'input>> = {
    <l:@L> <i:VarArray> "=" <e:Expr> <r:@R> => {
        let (id, s) = i;
        Box::new(CStmt::Assign((l, r), id, s, e))
    },

    // inc / dec
    <l:@L> <i:VarArray> "++" <r:@R> => {
        let (id, s) = i;
        Box::new(CStmt::Assign((l, r), id, s,
                               CExpr::BinOp((l, r),
                                            COp::Add,
                                            Box::new(CExpr::Ident((l, r), id)),
                                            Box::new(CExpr::Int((l, r), 1)))))
    },
    <l:@L> "++" <i:VarArray> <r:@R> => {
        let (id, s) = i;
        Box::new(CStmt::Assign((l, r), id, s,
                               CExpr::BinOp((l, r),
                                            COp::Add,
                                            Box::new(CExpr::Ident((l, r), id)),
                                            Box::new(CExpr::Int((l, r), 1)))))
    },
    <l:@L> <i:VarArray> "--" <r:@R> => {
        let (id, s) = i;
        Box::new(CStmt::Assign((l, r), id, s,
                               CExpr::BinOp((l, r),
                                            COp::Sub,
                                            Box::new(CExpr::Ident((l, r), id)),
                                            Box::new(CExpr::Int((l, r), 1)))))
    },
    <l:@L> "--" <i:VarArray> <r:@R> => {
        let (id, s) = i;
        Box::new(CStmt::Assign((l, r), id, s,
                               CExpr::BinOp((l, r),
                                            COp::Sub,
                                            Box::new(CExpr::Ident((l, r), id)),
                                            Box::new(CExpr::Int((l, r), 1)))))
    },
};

// expression

pub Expr: CExpr<'input> = {
    Expr1 => *<>,
};

Expr1: Box<CExpr<'input>> = {
    <l:@L> <e1:Expr1> "||" <e2:Expr2> <r:@R> => Box::new(CExpr::BinOp((l, r), COp::Or, e1, e2)),
    Expr2,
};

Expr2: Box<CExpr<'input>> = {
    <l:@L> <e1:Expr2> "&&" <e2:Expr3> <r:@R> => Box::new(CExpr::BinOp((l, r), COp::And, e1, e2)),
    Expr3,
};

Expr3: Box<CExpr<'input>> = {
    <l:@L> <e1:Expr3> <op:ExprOpRel2> <e2:Expr4> <r:@R> => Box::new(CExpr::BinOp((l, r), op, e1, e2)),
    Expr4,
};

Expr4: Box<CExpr<'input>> = {
    <l:@L> <e1:Expr4> <op:ExprOpRel> <e2:Expr5> <r:@R> => Box::new(CExpr::BinOp((l, r), op, e1, e2)),
    Expr5,
};

Expr5: Box<CExpr<'input>> = {
    <l:@L> <e1:Expr5> <op:ExprOp> <e2:Factor> <r:@R> => Box::new(CExpr::BinOp((l, r), op, e1, e2)),
    Factor,
};

Factor: Box<CExpr<'input>> = {
    <l:@L> <e1:Factor> <op:FactorOp> <e2:Term> <r:@R> => Box::new(CExpr::BinOp((l, r), op, e1, e2)),
    Term,
};

Term: Box<CExpr<'input>> = {
    <l:@L> <x:Int> <r:@R> => Box::new(CExpr::Int((l, r), x)),
    <l:@L> <x:Float> <r:@R> => Box::new(CExpr::Float((l, r), x)),
    <l:@L> <x:Str> <r:@R> => Box::new(CExpr::Str((l, r), x)),
    <l:@L> <x:Char> <r:@R> => Box::new(CExpr::Char((l, r), x)),

    // variable
    <l:@L> <i:Ident> <r:@R> => Box::new(CExpr::Ident((l, r), i)),

    // unary op
    <l:@L> <op:TermOp> <t:Term> <r:@R> => Box::new(CExpr::UnOp((l, r), op, t)),

    // call
    <l:@L> <i:Ident> "(" <p:Comma<Expr1>> ")" <r:@R> => Box::new(CExpr::Call((l, r), i, p)),

    // array index
    <l:@L> <i:Ident> "[" <e:Expr1> "]" <r:@R> => Box::new(CExpr::Index((l, r), i, e)),

    "(" <Expr1> ")",

    // ! => { errors.push(<>); Box::new(CExpr::Error) },
};

ExprOp: COp = {
    "+" => COp::Add,
    "-" => COp::Sub,
};

ExprOpRel: COp = {
    "<" => COp::Lt,
    "<=" => COp::Lte,
    ">" => COp::Gt,
    ">=" => COp::Gte,
};

ExprOpRel2: COp = {
    "==" => COp::Eq,
    "!=" => COp::Neq,
};

ExprOpLog: COp = {
    "&&" => COp::And,
    "||" => COp::Or,
};

FactorOp: COp = {
    "*" => COp::Mul,
    "/" => COp::Div,
};

TermOp: COp = {
    "-" => COp::Neg,
    "!" => COp::Not,
};

// simple stuff

Int: CInt = {
    TInt => i32::from_str(<>).unwrap(),
};

Size: usize = {
    TInt => usize::from_str(<>).unwrap(),
};

Float: CFloat = {
    TFloat => f32::from_str(<>).unwrap(),
};

Str: CString<'input> = {
    TString => <>.get(1..(<>.len() - 1)).unwrap().chars(),
};

Char: CChar = {
    TChar => match <> {
        "'\\0'" => '\0',
        "'\\n'" => '\n',
        _ => <>.chars().nth(1).unwrap(),
    }
};

Ident: &'input str = {
    TIdent => <>,
};

Type: CType = {
    "int" => CType::Int,
    "char" => CType::Char,
    "float" => CType::Float,
};
