use std::str::FromStr;
use ast::*;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, (usize, &'input str), ()>>);


// macros

Comma<T>: Vec<T> = {
    <h:(<T> ",")*> <t:T?> =>
        h.into_iter().chain(t).collect()
};


// token precedence

match {
    "void",
    "return",
    "extern",
    "if",
    "else",
    "while",
    "for",
    "continue",
    "break",

    "int",
    "char",

} else {
    r"[0-9]+" => TInt,

} else {
    r"[a-zA-Z][a-zA-Z0-9]*" => TIdent,

    _
}


// production rules

pub Prog: CProg<'input> = <(<ProgElem>)*>;

ProgElem: Box<CProgElem<'input>> = {
    // <l:VarDecl> ";" => Box::new(CProgElem::VarDecl(l)), // TODO
    <l:Proto> ";" => Box::new(CProgElem::Proto(l)),
    // "extern" <p:ProtoDecl> => Box::new(p), // TODO
    Func => Box::new(CProgElem::Func(<>)),
};

Proto: Box<CProto<'input>> = {
    FuncType Comma<FuncDecl> => Box::new((<>)),
};

FuncDecl: Box<(CIdent<'input>, Vec<Box<CParam<'input>>>)> = {
    <id:Ident> "(" <param:FuncParam> ")" => Box::new((id, param)),
};

Func: Box<CFunc<'input>> = {
    <ret:FuncType> <id:Ident> "(" <param:FuncParam> ")" "{" <decl:(<VarDecl> ";")*> <stmt:(<Stmt> ";")*> "}" =>
        Box::new(CFunc {
            ret_type: ret,
            name: id,
            params: param,
            decls: decl,
            stmts: stmt,
        }),
};

FuncType: Option<CType> = {
    Type => Some(<>),
    "void" => None,
};

FuncParam: Vec<Box<CParam<'input>>> = {
    Comma<Param>,
    "void" => Vec::new(),
};

Param: Box<CParam<'input>> = {
    Type Ident => Box::new((<>)),
};

VarDecl: Box<CVarDecl<'input>> = {
    Type Comma<Ident> => Box::new((<>)),
};

Stmt: Box<CStmt<'input>> = {
    <l:@L> <i:Ident> "=" <e:Expr> <r:@R> => Box::new(CStmt::Assign((l, r), i, e)),
    <l:@L> "return" <e:Expr> <r:@R> => Box::new(CStmt::Return((l, r), Some(e))),
    <l:@L> "return" <r:@R> => Box::new(CStmt::Return((l, r), None)),
    // ! => { errors.push(<>); Box::new(CStmt::Error) },
};

Expr: Box<CExpr<'input>> = {
    Expr ExprOp Factor => Box::new(CExpr::BinOp(<>)),
    <i:Ident> "(" <p:Comma<Expr>> ")" => Box::new(CExpr::Call(i, p)),
    Factor,
};

Factor: Box<CExpr<'input>> = {
    Factor FactorOp Term => Box::new(CExpr::BinOp(<>)),
    Term,
};

ExprOp: COp = {
    "+" => COp::Add,
    "-" => COp::Sub,
};

FactorOp: COp = {
    "*" => COp::Mul,
    "/" => COp::Div,
};

Term: Box<CExpr<'input>> = {
    Num => Box::new(CExpr::Number(<>)),
    Ident => Box::new(CExpr::Ident(<>)),
    "(" <Expr> ")",
    // ! => { errors.push(<>); Box::new(CExpr::Error) },
};

Num: i32 = {
    TInt => i32::from_str(<>).unwrap(),
};

Ident: &'input str = {
    TIdent => <>
};

Type: CType = {
    "int" => CType::Int,
    "char" => CType::Char,
};
