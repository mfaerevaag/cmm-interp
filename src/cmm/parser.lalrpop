use std::str::FromStr;
use ast::*;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, (usize, &'input str), ()>>);

// top level rule

pub Prog = Func;

// util rules

Comma<T>: Vec<T> = {
    <h:(<T> ",")*> <t:T?> =>
        h.into_iter().chain(t).collect()
};


// production rules

Func: Box<CFunc<'input>> = {
    <rt:FuncType> <id:Ident> "(" <pt:ParamDecl> ")" "{" <d:(<Decl> ";")*> <s:(<Stmt> ";")*> "}" =>
        Box::new(CFunc {
            ret_type: rt,
            name: id,
            params: pt,
            decls: d,
            stmts: s
        }),
};

FuncType: Option<CType> = {
    Type => Some(<>),
    "void" => None,
};

ParamDecl: Vec<Box<CParam<'input>>> = {
    Comma<Param>,
    "void" => Vec::new(),
};

Param: Box<CParam<'input>> = {
    Type Ident => Box::new((<>)),
};

Decl: Box<CDecl<'input>> = {
    Type Comma<Ident> => Box::new((<>)),
};

Stmt: Box<CStmt<'input>> = {
    <l:Ident> "=" <r:Expr> => Box::new(CStmt::Assign(l, r)),
    "return" <r:Expr> => Box::new(CStmt::Return(Some(r))),
    "return" => Box::new(CStmt::Return(None)),
    // ! => { errors.push(<>); Box::new(CStmt::Error) },
};

Expr: Box<CExpr<'input>> = {
    Expr ExprOp Factor => Box::new(CExpr::BinOp(<>)),
    Factor,
};

Factor: Box<CExpr<'input>> = {
    Factor FactorOp Term => Box::new(CExpr::BinOp(<>)),
    Term,
};

ExprOp: COp = {
    "+" => COp::Add,
    "-" => COp::Sub,
};

FactorOp: COp = {
    "*" => COp::Mul,
    "/" => COp::Div,
};

Term: Box<CExpr<'input>> = {
    Num => Box::new(CExpr::Number(<>)),
    Ident => Box::new(CExpr::Ident(<>)),
    "(" <Expr> ")",
    // ! => { errors.push(<>); Box::new(CExpr::Error) },
};

Num: i32 = {
    r"\d+" => i32::from_str(<>).unwrap(),
};

Ident: &'input str = {
    r"[a-zA-Z][a-zA-Z0-9]*" => <>
};

Type: CType = {
    "int" => CType::Int,
    "char" => CType::Char,
};
